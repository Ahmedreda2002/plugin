plugins {
	id 'java'
	id 'groovy'
	id 'idea'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.morpheusdata'
version = '1.2.1'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
	withSourcesJar()
	withJavadocJar()
}

repositories {
	mavenLocal()
	mavenCentral()
}

configurations {
	provided
}

dependencies {
	provided "com.morpheusdata:morpheus-plugin-api:${morphPluginApiVersion}"
	provided "org.codehaus.groovy:groovy-all:${groovyVersion}"

	implementation "org.slf4j:slf4j-api:${slf4jVersion}"

	testImplementation "com.morpheusdata:morpheus-plugin-api:${morphPluginApiVersion}"
	testImplementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
	testImplementation "org.spockframework:spock-core:${spockVersion}"
}

sourceSets {
	main {
		compileClasspath += configurations.provided
	}
	test {
		compileClasspath += configurations.provided
		runtimeClasspath += configurations.provided
	}
}

jar {
	manifest {
		attributes(
			'Plugin-Class': 'com.morpheusdata.reports.ReportsPlugin',
			'Plugin-Version': archiveVersion.get()
		)
	}
}

tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	archiveClassifier.set('all')
	destinationDirectory = file("$rootDir/plugins")
}

tasks.assemble.dependsOn tasks.shadowJar
